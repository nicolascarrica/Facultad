programa ejemplo
procesos
  proceso Maximo (E id:numero; ES idMax:numero; E cant:numero; ES cantMax:numero)
  comenzar
    si(cant>cantMax)
      cantMax:=cant
      idMax:=id
  fin
  proceso JuntarFlores(ES flores:numero; ES aux:numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores:=flores+1
      aux:=aux+1
  fin
  proceso DepositarDeAUna (E cant:numero)
  variables
    miAv, miCa:numero
  comenzar
    miAv:=PosAv
    miCa:=PosCa
    repetir cant
      BloquearEsquina(25,25)
      Pos(25,25)
      depositarFlor
      Pos(miAv, miCa)
      LiberarEsquina(25,25)
  fin
  
  proceso Rectangulo(E alto:numero; E ancho:numero; ES flores:numero)
  variables
    aux:numero
  comenzar
    repetir 2
      aux:=0
      repetir alto
        JuntarFlores(flores, aux)
        mover
      DepositarDeAUna(aux)
      derecha
      aux:=0
      repetir ancho
        JuntarFlores(flores, aux)
        mover
      DepositarDeAUna(aux)
      derecha    
  fin
areas
  area1: AreaP (1,1,6,11)
  area2: AreaP(7,1,12,11)
  area3: AreaP(13,1,18,11)
  areaF: AreaP(24,24,24,24)
  AreaDep: AreaPC(25,25,25,25)
robots
  robot recolector
  variables
    id:numero
    ancho, alto:numero
    flores:numero
  comenzar
    flores:=0
    RecibirMensaje(id, rf)
    RecibirMensaje(alto, rf)
    RecibirMensaje(ancho, rf)
    Rectangulo(alto, ancho, flores)
    EnviarMensaje(id, rf)
    EnviarMensaje(flores, rf)
  fin
  robot coordinador
  variables
    alto, ancho:numero
    id:numero
    flores, floresMax, idMax:numero
  comenzar
    floresMax:=0
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    id:=1
    repetir 3
      Random(alto,1,10)
      Random(ancho,1, 5)
      si(id=1)
        EnviarMensaje(alto, r1)
        EnviarMensaje(ancho, r1)
      sino
        si(id=2)
          EnviarMensaje(alto, r2)
          EnviarMensaje(ancho, r2)
        sino
          EnviarMensaje(alto, r3)
          EnviarMensaje(ancho, r3)
      id:=id+1
    repetir 3
      RecibirMensaje(id, *)
      RecibirMensaje(flores, *) 
      Maximo(id, idMax, flores, floresMax) 
    Informar(idMax)
  fin
variables
  r1,r2,r3: recolector
  rf: coordinador
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r1, AreaDep)
  AsignarArea(r2, area2)
  AsignarArea(r2, AreaDep)
  AsignarArea(r3, area3)
  AsignarArea(r3, AreaDep)
  AsignarArea(rf, areaF)
  Iniciar(r1, 1,1)
  Iniciar(r2, 7,1)
  Iniciar(r3, 13,1)
  Iniciar(rf, 24,24)
fin