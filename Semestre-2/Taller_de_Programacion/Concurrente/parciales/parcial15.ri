programa ejemplo
procesos
  proceso JuntarFlores
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
  fin
areas
  cuadrante: AreaPC (10,10,30,30)
  area1: AreaP(1,1,1,1)
  area2: AreaP(2,1,2,1)
  area3: AreaP(3,1,3,1)
  areaF: AreaP(9,9,9,9)
robots
  robot florero
  variables
    id:numero
    intentos:numero
    miCa, miAv, calle, avenida:numero
  comenzar
    RecibirMensaje(id, rf)
    Random(intentos, 2,5)
    EnviarMensaje(id, rf)
    EnviarMensaje(intentos, rf)
    miCa:=PosCa
    miAv:=PosAv
    repetir intentos
      EnviarMensaje(id, rf)
      RecibirMensaje(avenida, rf)
      RecibirMensaje(calle, rf)
      BloquearEsquina(avenida,calle)
      Pos(avenida, calle)
      JuntarFlores
      Pos(miAv, miCa)
      LiberarEsquina(avenida,calle)
  fin
  robot fiscal
  variables
    id:numero
    calle, avenida:numero
    int1,int2,int3,totalIntentos:numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    repetir 3
      RecibirMensaje(id, *)
      si (id=1)
        RecibirMensaje(int1, r1)
      sino
        si(id=2)
          RecibirMensaje(int2, r2)
        sino
          RecibirMensaje(int3,r3)
    totalIntentos:=int1+int2+int3  
    mientras ((int1 > 0) | (int2 > 0) | (int3 > 0))
      Random(avenida, 10,30)
      Random(calle, 10,30)
      RecibirMensaje(id,*)
      si(id=1)
        EnviarMensaje(avenida, r1)
        EnviarMensaje(calle,r1)
        int1:=int1-1
      sino
        si(id=2)
          EnviarMensaje(avenida, r2)
          EnviarMensaje(calle, r2)
          int2:=int2-1
        sino
          EnviarMensaje(avenida, r3)
          EnviarMensaje(calle, r3)
          int3:=int3-1
    Informar(totalIntentos)
  fin
variables
  r1,r2,r3: florero
  rf:fiscal
comenzar
  AsignarArea(r1, cuadrante)
  AsignarArea(r1, area1)
  AsignarArea(r2, cuadrante)
  AsignarArea(r2, area2)
  AsignarArea(r3, cuadrante)
  AsignarArea(r3, area3)
  AsignarArea(rf, areaF)
  Iniciar(r1, 1,1)
  Iniciar(r2, 2,1)
  Iniciar(r3, 3,1)
  Iniciar(rf, 9,9)
fin