programa ejemplo
procesos
  proceso JuntarFlores(ES flores:numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores:=flores+1
  fin
  
  proceso Lado(ES flores:numero)
  comenzar
    repetir 5
      JuntarFlores(flores)
      mover
    JuntarFlores(flores)
  fin
  
  proceso Maximo(E id:numero; ES idMax:numero; E cantidad:numero; ES cantMax:numero)
  comenzar
    si(cantidad>cantMax)
      cantMax:=cantidad
      idMax:=id
  fin
  
  proceso DepositarDeAUna(E flores:numero)
  variables
    miAv, miCa:numero
  comenzar
    miAv:=PosAv
    miCa:=PosCa
    repetir flores
      BloquearEsquina(10,10)
      Pos(10,10)
      depositarFlor
      Pos(miAv, miCa)
      LiberarEsquina(10,10)
  fin
  
areas
  area1: AreaP (1,1,6,6)
  area2: AreaP (7,1,12,6)
  area3: AreaP (13,1,18,6)
  areaC: AreaP (20,1,20,1)
  areaDep: AreaPC(10,10,10,10)
robots
  robot trabajador
  variables
    id, idMax:numero
    flores:numero
    ok:boolean
  comenzar
    flores:=0
    RecibirMensaje(id, rc)
    repetir 4
      Lado(flores)
      EnviarMensaje(V, rc)
      RecibirMensaje(ok, rc)
      derecha
    EnviarMensaje(id, rc)
    EnviarMensaje(flores, rc)
    RecibirMensaje(idMax, rc)
    si(id<>idMax)
      DepositarDeAUna(flores)
    EnviarMensaje(V, rc)
      
  fin
  robot coordinador
  variables
    id, idMax:numero
    ok:boolean
    flores, cantMax:numero
  comenzar
    cantMax:=0
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    repetir 4
      repetir 3
        RecibirMensaje(ok, *)
      EnviarMensaje(ok,r1)
      EnviarMensaje(ok,r2)
      EnviarMensaje(ok,r3)
    repetir 3
      RecibirMensaje(id, *)
      si(id=1)
        RecibirMensaje(flores, r1)
        Maximo(id, idMax, flores, cantMax)
      sino
        si(id=2)
          RecibirMensaje(flores, r2)
          Maximo(id, idMax, flores, cantMax)
        sino
          RecibirMensaje(flores, r3)
          Maximo(id, idMax, flores, cantMax)
    EnviarMensaje(idMax, r1)
    EnviarMensaje(idMax, r2)
    EnviarMensaje(idMax, r3)
    repetir 3
      RecibirMensaje(ok, *)
    Informar('ganador', idMax) 
  fin
variables
  r1,r2,r3: trabajador
  rc:coordinador
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r1, areaDep)
  AsignarArea(r2, area2)
  AsignarArea(r2, areaDep)
  AsignarArea(r3, area3)
  AsignarArea(r3, areaDep)
  AsignarArea(rc, areaC)
  Iniciar(r1, 1,1)
  Iniciar(r2, 7,1)
  Iniciar(r3, 13,1)
  Iniciar(rc, 20,1)
fin